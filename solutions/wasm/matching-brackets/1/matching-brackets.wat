;; Dedicated to Shree DR.MDD
(module
  (memory (export "mem") 1)

  (func (export "isPaired") (param $seq i32) (param $size i32) (result i32)
    (local $scan i32) (local $stackTop i32) (local $curr i32)
    (local.set $scan (i32.const 0)) (local.set $stackTop (i32.const 0))
    (loop $loopScan
      (local.set $curr (i32.load8_u (i32.add (local.get $seq) (local.get $scan))))
      (if (i32.eq (local.get $curr) (i32.const 40))
        (then (i32.store8 (i32.add (local.get $seq) (local.get $stackTop)) (i32.const 41))
          (local.set $stackTop (i32.add (local.get $stackTop) (i32.const 1)))))
      (if (i32.eq (local.get $curr) (i32.const 91))
        (then (i32.store8 (i32.add (local.get $seq) (local.get $stackTop)) (i32.const 93))
          (local.set $stackTop (i32.add (local.get $stackTop) (i32.const 1)))))
      (if (i32.eq (local.get $curr) (i32.const 123))
        (then (i32.store8 (i32.add (local.get $seq) (local.get $stackTop)) (i32.const 125))
          (local.set $stackTop (i32.add (local.get $stackTop) (i32.const 1)))))
      (if (i32.or (i32.or (i32.eq (local.get $curr) (i32.const 41))
        (i32.eq (local.get $curr) (i32.const 93)))
        (i32.eq (local.get $curr) (i32.const 125)))
        (then (local.set $stackTop (i32.sub (local.get $stackTop) (i32.const 1)))
          (if (i32.ne (local.get $curr)
            (i32.load8_u (i32.add (local.get $seq) (local.get $stackTop))))
            (then (return (i32.const 0))))))
      (local.set $scan (i32.add (local.get $scan) (i32.const 1)))
      (br_if $loopScan (i32.ne (local.get $scan) (local.get $size)))
    )
    (i32.eqz (local.get $stackTop))
  )
)
