.globl tick

tick:
        beqz    $a0, return
        move    $t8, $zero
        lw      $t9, 0($a2)
        addi    $a2, $a2, 4 

outer:
        subi    $a0, $a0, 1
        move    $t0, $t8
        move    $t8, $t9
        move    $t9, $zero
        beqz    $a0, process_row
        lw      $t9, 0($a2)
        addi    $a2, $a2, 4 

process_row:
        move    $v0, $zero
        move    $v1, $zero
        move    $t1, $t8
        move    $t2, $t9
        move    $t6, $zero
        andi    $t7, $t0, 1
        andi    $t4, $t2, 1
        add     $t7, $t7, $t4
        andi    $t4, $t1, 1
        add     $t7, $t7, $t4
        beqz    $a1, write

inner:
        srl     $t0, $t0, 1
        srl     $t1, $t1, 1
        srl     $t2, $t2, 1
        move    $t3, $t4
        add     $t5, $t6, $t7
        move    $t6, $t7
        andi    $t7, $t0, 1
        andi    $t4, $t2, 1
        add     $t7, $t7, $t4
        andi    $t4, $t1, 1
        add     $t7, $t7, $t4
        add     $t5, $t5, $t7
        sub     $t5, $t5, $t3
        or      $t5, $t5, $t3
        li      $t3, 3
        seq     $t5, $t5, $t3
        sllv    $t5, $t5, $v1
        or      $v0, $v0, $t5
        addi    $v1, $v1, 1
        bne     $v1, $a1, inner

write:
        sw      $v0, 0($a3)
        addi    $a3, $a3, 4
        bnez    $a0, outer

return:
        jr      $ra
